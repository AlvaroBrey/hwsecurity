apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

dependencies {
    implementation("androidx.core:core-ktx:1.12.0")
    implementation 'androidx.lifecycle:lifecycle-runtime:2.6.2'
    implementation "androidx.appcompat:appcompat:1.6.1"
    compileOnly 'androidx.annotation:annotation:1.7.0'
    implementation("androidx.annotation:annotation-experimental:1.3.1")
    compileOnly 'io.sentry:sentry-android:2.3.1'

    api 'com.google.auto.value:auto-value-annotations:1.10.4'
    annotationProcessor 'com.google.auto.value:auto-value:1.10.4'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.9'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.mockito:mockito-core:2.18.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    namespace "de.cotech.hw.core"

    defaultConfig {
        minSdkVersion 24
        versionName rootProject.ext.hwSdkVersionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

// https://developer.android.com/studio/build/maven-publish-plugin
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'de.cotech'
                artifactId = 'hwsecurity'
                version = android.defaultConfig.versionName

                pom {
                    url = 'https://hwsecurity.dev'
                    licenses {
                        license {
                            name = 'Commercial'
                            url = 'https://hwsecurity.dev/sales/'
                            distribution = 'repo'
                        }
                        license {
                            name = 'GNU General Public License, version 3'
                            url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        }
                    }
                    organization {
                        name = 'Confidential Technologies GmbH'
                        url = 'https://www.cotech.de'
                    }
                }
            }
        }
        /*
         * To upload release, create file gradle.properties in ~/.gradle/ with this content:
         *
         * cotechMavenName=xxx
         * cotechMavenPassword=xxx
         */
        if (project.hasProperty('cotechMavenName') && project.hasProperty('cotechMavenPassword')) {
            println "Found cotechMavenName, cotechMavenPassword in gradle.properties!"

            repositories {
                maven {
                    credentials {
                        username cotechMavenName
                        password cotechMavenPassword
                    }
                    url = "https://maven.cotech.de"
                }
            }
        }
    }
}

dokka {
    // uses locally built dokka extension
    dokkaFatJar = files('libs/dokka-hugo-fatjar-0.9.17.jar')
    // does not work correctly with Maven:
    //dokkaFatJar = 'de.cotech:dokka-hugo-fatjar:0.9.17'
    moduleName = 'hwsecurity'
    outputFormat = "hugo"
    outputDirectory = "$projectDir/../../hwsecurity.dev/content/reference"
    sourceDirs = files('src/main/java')

    packageOptions {
        prefix = "de.cotech.hw.internal"
        suppress = true
    }
}
