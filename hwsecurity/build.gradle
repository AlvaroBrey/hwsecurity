apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

dependencies {
    compileOnly 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.0.0'

    api 'com.google.auto.value:auto-value-annotations:1.6.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'

    // Unit tests in the local JVM with Robolectric
    // https://developer.android.com/training/testing/unit-testing/local-unit-tests.html
    // http://robolectric.org/getting-started/
    // http://www.vogella.com/tutorials/Robolectric/article.html
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.2.2'
    testImplementation 'org.mockito:mockito-core:2.18.0'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 14
        versionCode rootProject.ext.hwSdkVersionCode
        versionName rootProject.ext.hwSdkVersionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'de.cotech'
            artifactId = 'hwsecurity'
            version = android.defaultConfig.versionName

            from components.android

            pom {
                url = 'https://hwsecurity.dev'
                licenses {
                    license {
                        name = 'Commercial'
                        url = 'https://hwsecurity.dev/sales/'
                        distribution = 'repo'
                    }
                    license {
                        name = 'GNU General Public License, version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                organization {
                    name = 'Confidential Technologies GmbH'
                    url = 'https://www.cotech.de'
                }
            }
        }
    }
    /*
     * To upload release, create file gradle.properties in ~/.gradle/ with this content:
     *
     * cotechMavenName=xxx
     * cotechMavenPassword=xxx
     */
    if (project.hasProperty('cotechMavenName') && project.hasProperty('cotechMavenPassword')) {
        println "Found cotechMavenName, cotechMavenPassword in gradle.properties!"

        repositories {
            maven {
                credentials {
                    username cotechMavenName
                    password cotechMavenPassword
                }
                url = "https://maven.cotech.de"
            }
        }
    }
}

dokka {
    // uses locally built dokka extension
    dokkaFatJar = files('libs/dokka-hugo-fatjar-0.9.17.jar')
    // does not work correctly with Maven:
    //dokkaFatJar = 'de.cotech:dokka-hugo-fatjar:0.9.17'
    outputFormat = "hugo"
    outputDirectory = "$buildDir/dokka/reference"
    sourceDirs = files('src/main/java')

    packageOptions {
        prefix = "de.cotech.hw.internal"
        suppress = true
    }
}
