apply plugin: 'com.android.library'
apply plugin: 'digital.wup.android-maven-publish'
apply plugin: 'org.jetbrains.dokka-android'

dependencies {
    if (rootProject.ext.has("hwSdkIncludeAsSubmodule") && rootProject.hwSdkIncludeAsSubmodule) {
        compileOnly project(':hwsecurity:hwsecurity-openpgp')
        //compileOnly project(':hwsecurity:hwsecurity-piv')
    } else {
        compileOnly project(':hwsecurity-openpgp')
        //compileOnly project(':hwsecurity-piv')
    }

    implementation 'de.cotech:nfc-sweetspot:1.1'

    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.2'

    api 'com.google.auto.value:auto-value-annotations:1.6.2'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.6'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion 14
        versionName rootProject.ext.hwSdkVersionName
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Do not abort build if lint finds errors
    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        mavenAar(MavenPublication) {
            groupId = 'de.cotech'
            artifactId = 'hwsecurity-ui'
            version = android.defaultConfig.versionName

            from components.android

            pom {
                url = 'https://hwsecurity.dev'
                licenses {
                    license {
                        name = 'Commercial'
                        url = 'https://hwsecurity.dev/sales/'
                        distribution = 'repo'
                    }
                }
                organization {
                    name = 'Confidential Technologies GmbH'
                    url = 'https://www.cotech.de'
                }
            }
        }
    }
    /*
     * To upload release, create file gradle.properties in ~/.gradle/ with this content:
     *
     * cotechMavenName=xxx
     * cotechMavenPassword=xxx
     */
    if (project.hasProperty('cotechMavenName') && project.hasProperty('cotechMavenPassword')) {
        println "Found cotechMavenName, cotechMavenPassword in gradle.properties!"

        repositories {
            maven {
                credentials {
                    username cotechMavenName
                    password cotechMavenPassword
                }
                url = "https://maven.cotech.de"
            }
        }
    }
}

dokka {
    outputFormat = "hugo"
    outputDirectory = "$buildDir/dokka/reference"
    sourceDirs = files('src/main/java')

    packageOptions {
        prefix = "de.cotech.hw.ui.internal"
        suppress = true
    }
}
